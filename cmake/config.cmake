## any update for config.cmake should sync with config_for_fetch_content.cmake
## the target `yalantinglibs::yalantinglibs` in config.cmake should modify as `yalantinglibs` in config_for_fetch_content.cmake

if (YLT_IS_IMPORTED_BY_FIND_PACKAGE)
    message("YLT is imported by find_package")
    set(ylt_target_name "yalantinglibs::yalantinglibs")
else()
    if(NOT CMAKE_PROJECT_NAME STREQUAL "yaLanTingLibs")
        message("YLT is imported by fetchContent or add_subdirectory")
    endif()
    set(ylt_target_name "yalantinglibs")
endif()

message(STATUS "-------------YLT CONFIG SETTING-------------")

find_package(Threads REQUIRED)
if(NOT CMAKE_PROJECT_NAME STREQUAL "yaLanTingLibs")
    target_link_libraries(${ylt_target_name} INTERFACE Threads::Threads)
    if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
        target_link_libraries(${ylt_target_name} INTERFACE dl)
    endif()
    # Add Windows-specific libraries for MinGW-w64
    if(CMAKE_CXX_COMPILER_ID STREQUAL "GNU" AND CMAKE_SYSTEM_NAME MATCHES "Windows")
        target_link_libraries(${ylt_target_name} INTERFACE ws2_32 mswsock dbghelp)
    endif()
else()
    # Add Windows-specific libraries for MinGW-w64 when building as main project
    if(CMAKE_CXX_COMPILER_ID STREQUAL "GNU" AND CMAKE_SYSTEM_NAME MATCHES "Windows")
        link_libraries(ws2_32 mswsock dbghelp)
    endif()
endif()
option(YLT_ENABLE_DL "Enable dl support for stacktrace" ON)
message(STATUS "YLT_ENABLE_DL: ${YLT_ENABLE_DL}")
if (YLT_ENABLE_DL)
    if(CMAKE_PROJECT_NAME STREQUAL "yaLanTingLibs")
        add_compile_definitions("YLT_ENABLE_DL")
        link_libraries(${CMAKE_DL_LIBS})
    else ()
        target_link_libraries(${ylt_target_name} INTERFACE ${CMAKE_DL_LIBS})
        target_compile_definitions(${ylt_target_name}  INTERFACE "YLT_ENABLE_DL")
    endif ()
endif()
option(YLT_ENABLE_SSL "Enable ssl support" OFF)
message(STATUS "YLT_ENABLE_SSL: ${YLT_ENABLE_SSL}")
if (YLT_ENABLE_SSL)
    find_package(OpenSSL REQUIRED)
    if(CMAKE_PROJECT_NAME STREQUAL "yaLanTingLibs")
        add_compile_definitions("YLT_ENABLE_SSL")
        link_libraries(OpenSSL::SSL OpenSSL::Crypto)
    else ()
        target_compile_definitions(${ylt_target_name} INTERFACE "YLT_ENABLE_SSL")
        target_link_libraries(${ylt_target_name} INTERFACE OpenSSL::SSL OpenSSL::Crypto)
    endif ()
endif ()
option(YLT_ENABLE_NTLS "Enable NTLS support with Tongsuo" OFF)
message(STATUS "YLT_ENABLE_NTLS: ${YLT_ENABLE_NTLS}")
if (YLT_ENABLE_NTLS)
    if (NOT YLT_ENABLE_SSL)
        message(FATAL_ERROR "YLT_ENABLE_NTLS requires YLT_ENABLE_SSL to be ON")
    endif()
    # Check if OpenSSL is actually Tongsuo (which supports NTLS)
    # We check for NTLS-specific functions that only exist in Tongsuo
    include(CheckCSourceCompiles)
    set(CMAKE_REQUIRED_LIBRARIES ${OPENSSL_SSL_LIBRARY} ${OPENSSL_CRYPTO_LIBRARY})
    set(CMAKE_REQUIRED_INCLUDES ${OPENSSL_INCLUDE_DIR})
    enable_language(C)
    check_c_source_compiles("
        #include <openssl/ssl.h>
        int main() {
            SSL_CTX *ctx = NULL;
            SSL_CTX_enable_ntls(ctx);
            return 0;
        }
    " HAVE_NTLS_SUPPORT)
    unset(CMAKE_REQUIRED_LIBRARIES)
    unset(CMAKE_REQUIRED_INCLUDES)
    
    if (NOT HAVE_NTLS_SUPPORT)
        message(WARNING "OpenSSL library does not support NTLS. Please use Tongsuo library.")
        message(WARNING "NTLS code may not compile correctly without Tongsuo.")
    else()
        message(STATUS "NTLS support detected in OpenSSL/Tongsuo library")
    endif()
    if(CMAKE_PROJECT_NAME STREQUAL "yaLanTingLibs")
        message(STATUS "NTLS code will be compiled (OPENSSL_NO_NTLS not defined)")
    else ()
        target_compile_definitions(${ylt_target_name} INTERFACE "YLT_ENABLE_NTLS")
    endif ()
else ()
    if(CMAKE_PROJECT_NAME STREQUAL "yaLanTingLibs")
        add_compile_definitions("OPENSSL_NO_NTLS")
    else ()
        target_compile_definitions(${ylt_target_name} INTERFACE "OPENSSL_NO_NTLS")
    endif ()
endif ()
option(YLT_ENABLE_IBV "Enable ibverbs support" OFF)
if (YLT_ENABLE_IBV)
    message(STATUS "Enable ibverbs support")
    if(CMAKE_PROJECT_NAME STREQUAL "yaLanTingLibs")
        add_compile_definitions("YLT_ENABLE_IBV")
        link_libraries(-libverbs)
    else ()
        target_compile_definitions(${ylt_target_name} INTERFACE "YLT_ENABLE_IBV")
        target_link_libraries(${ylt_target_name} INTERFACE -libverbs)
    endif ()
endif ()

option(YLT_ENABLE_PMR "Enable pmr support" OFF)
message(STATUS "YLT_ENABLE_PMR: ${YLT_ENABLE_PMR}")
if (YLT_ENABLE_PMR)
    if(CMAKE_PROJECT_NAME STREQUAL "yaLanTingLibs")
        add_compile_definitions("YLT_ENABLE_PMR" IGUANA_ENABLE_PMR)
    else ()
        target_compile_definitions(${ylt_target_name} INTERFACE "YLT_ENABLE_PMR" IGUANA_ENABLE_PMR)
    endif ()
endif ()

option(YLT_ENABLE_IO_URING "Enable io_uring" OFF)
message(STATUS "YLT_ENABLE_IO_URING: ${YLT_ENABLE_IO_URING}")
if (YLT_ENABLE_IO_URING)
  find_package(uring REQUIRED)
  message(STATUS "Use IO_URING for all I/O in linux")
  if(CMAKE_PROJECT_NAME STREQUAL "yaLanTingLibs")
      add_compile_definitions(ASIO_HAS_IO_URING ASIO_DISABLE_EPOLL ASIO_HAS_FILE YLT_ENABLE_FILE_IO_URING)
      link_libraries(uring)
  else ()
      target_compile_definitions(${ylt_target_name} INTERFACE ASIO_HAS_IO_URING ASIO_DISABLE_EPOLL ASIO_HAS_FILE YLT_ENABLE_FILE_IO_URING)
      target_link_libraries(${ylt_target_name} INTERFACE uring)
  endif ()
endif()

option(YLT_ENABLE_FILE_IO_URING "Enable file io_uring" OFF)
if (NOT YLT_ENABLE_IO_URING)
  if(YLT_ENABLE_FILE_IO_URING)
    find_package(uring REQUIRED)
    message(STATUS "YLT: Enable io_uring for file I/O in linux")
    if(CMAKE_PROJECT_NAME STREQUAL "yaLanTingLibs")
        add_compile_definitions(ASIO_HAS_IO_URING ASIO_HAS_FILE "YLT_ENABLE_FILE_IO_URING")
        link_libraries(uring)
    else ()
        target_compile_definitions(${ylt_target_name} INTERFACE ASIO_HAS_IO_URING ASIO_HAS_FILE "YLT_ENABLE_FILE_IO_URING")
        target_link_libraries(${ylt_target_name} INTERFACE uring)
    endif ()
  endif()
endif()

option(YLT_ENABLE_STRUCT_PACK_UNPORTABLE_TYPE "enable struct_pack unportable type(like wchar_t)" OFF)
message(STATUS "YLT_ENABLE_STRUCT_PACK_UNPORTABLE_TYPE: ${YLT_ENABLE_STRUCT_PACK_UNPORTABLE_TYPE}")
if(YLT_ENABLE_STRUCT_PACK_UNPORTABLE_TYPE)
  add_compile_definitions(STRUCT_PACK_ENABLE_UNPORTABLE_TYPE)
endif()

option(YLT_ENABLE_STRUCT_PACK_OPTIMIZE "enable struct_pack optimize(but cost more compile time)" OFF)
message(STATUS "YLT_ENABLE_STRUCT_PACK_OPTIMIZE: ${YLT_ENABLE_STRUCT_PACK_OPTIMIZE}")
if(YLT_ENABLE_STRUCT_PACK_OPTIMIZE)
    if(CMAKE_PROJECT_NAME STREQUAL "yaLanTingLibs")
        add_compile_definitions(YLT_ENABLE_STRUCT_PACK_OPTIMIZE)
    else ()
        target_compile_definitions(${ylt_target_name} INTERFACE YLT_ENABLE_STRUCT_PACK_OPTIMIZE)
    endif ()
endif()
message(STATUS "--------------------------------------------")

